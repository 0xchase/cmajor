//
//     ,ad888ba,                              88
//    d8"'    "8b
//   d8            88,dba,,adba,   ,aPP8A.A8  88     The Cmajor Toolkit
//   Y8,           88    88    88  88     88  88
//    Y8a.   .a8P  88    88    88  88,   ,88  88     (C)2022 Sound Stacks Ltd
//     '"Y888Y"'   88    88    88  '"8bbP"Y8  88     https://cmajor.dev
//                                           ,88
//                                        888P"

/// std.levels

/**
    Utilities for calculating and applying smoothing ramps and gain levels.
    The `std::smoothing` namespace is parameterised. `FrameType` defaults to float32,
    but can be specified to allow support for different precision types (e.g. `float64`)
    as well as smoothed values for multi-channel use (e.g. `float32<2>`)
*/
namespace std::smoothing (using FrameType = float32)
{
    //==============================================================================
    /// Utilities for smoothing non-audio signals, such as control parameters.
    /// Holds a float value and is given a target to move towards over a given time.
    /// Call setTarget() to tell the object where to go and how many steps to take
    /// to get there, then repeatedly call getNext() to iterate to the target.
    struct SmoothedValue
    {
        /// Immediately resets the value to the given argument
        void reset (FrameType newValue)
        {
            this.value = newValue;
            this.target = newValue;
            this.increment = 0;
            this.remainingSteps = 0;
        }

        /// Gives the object a target value and the number of steps it should take
        /// to reach it.
        void setTarget (FrameType targetValue, int32 stepsToReachTarget)
        {
            stepsToReachTarget = max (1, stepsToReachTarget);

            this.target = targetValue;
            this.increment = (targetValue - this.currentValue()) / stepsToReachTarget;
            this.remainingSteps = stepsToReachTarget;
            this.value = targetValue;
        }

        /// Returns the current value and increments it towards the current target.
        FrameType getNext()
        {
            if (this.remainingSteps > 0)
            {
                this.remainingSteps--;
                return this.currentValue();
            }

            return this.value;
        }

        FrameType currentValue()
        {
            return this.value - this.increment * this.remainingSteps;
        }

        FrameType value, target, increment;
        int32 remainingSteps;
    }

    //==============================================================================
    /// Takes an input that receives floating point events, and produces a continuous
    /// output stream of floats which are smoothed by a specified amount.
    /// This is handy for smoothing plugin parameters
    processor SmoothedValueStream (float32 smoothingTimeSeconds = 0.2f, FrameType initialValue = 0.0f)
    {
        /// Receives a new desired value. The processor's output will ramp towards
        /// this target level over the length of time specified by smoothingTimeSeconds.
        input event FrameType in;
        /// This is the stream of smoothed output values
        output stream FrameType out;

        event in (FrameType newTarget)
        {
            smoothedValue.setTarget (newTarget, max (1, int32 (smoothingTimeSeconds / float32 (processor.period))));
        }

        void main()
        {
            loop
            {
                out <- smoothedValue.getNext();
                advance();
            }
        }

        void init()
        {
            smoothedValue.reset (initialValue);
        }

        SmoothedValue smoothedValue;
    }
}


//==============================================================================
/// Utilities relating to gain levels
namespace std::levels
{
    /// Converts a relative gain in decibels to a gain.
    T dBtoGain<T> (T decibels)
    {
        static_assert (T.isPrimitive && T.primitiveType.isFloat, "dbToGain() requires a floating point argument");
        return decibels > T(-100) ? T(10) ** (decibels * T(0.05)) : T();
    }

    /// Converts a gain to a relative number of decibels.
    T gainTodB<T> (T gainFactor)
    {
        static_assert (T.isPrimitive && T.primitiveType.isFloat, "gainTodB() requires a floating point argument");
        return gainFactor > 0 ? log10(gainFactor) * T(20) : T(-100);
    }

    //==============================================================================
    /// This helper processor simply passes its input through to its output with a
    /// constant gain applied.
    /// The FrameType parameter could be a float, or a vector of floats.
    /// See: DynamicGain, SmoothedGain
    graph ConstantGain (using FrameType, float32 gain)
    {
        input  stream FrameType  in;
        output stream FrameType  out;

        connection
            in * gain -> out;
    }

    //==============================================================================
    /// This helper processor takes two input streams - one for data and the other
    /// for a gain level, and emits the input signal with the gain applied.
    /// The FrameType parameter could be a float, or a vector of floats.
    /// See: ConstantGain, SmoothedGain
    graph DynamicGain (using FrameType)
    {
        input  stream float32    gain;
        input  stream FrameType  in;
        output stream FrameType  out;

        connection
            in * gain -> out;
    }

    //==============================================================================
    /// Takes an input stream of the given FrameType, and passes it through using
    /// a SmoothedGainParameter processor to apply a smoothed gain to it. The gain
    /// level is exposed via the 'volume' input.
    graph SmoothedGain (using FrameType, float32 smoothingTimeSeconds = 0.2f)
    {
        input  gainParameter.volume  volume;
        input  stream FrameType      in;
        output stream FrameType      out;

        node
            gainParameter = std::levels::SmoothedGainParameter (smoothingTimeSeconds);

        connection
            in * gainParameter.gain -> out;
    }

    //==============================================================================
    /// Takes input events which change the level in decibels, and produces an
    /// output stream of smoothed gain factors which can be applied to a signal.
    processor SmoothedGainParameter (float32 smoothingTimeSeconds = 0.2f)
    {
        /// This input is used to set a new desired level in dB. The output gain will ramp towards
        /// this target level over the length of time specified by smoothingTimeSeconds.
        input  event  float32 volume   [[ name: "Volume", unit: "dB", min: -85, max: 6, init: 0 ]];
        /// This output is the stream of smoothed gain levels.
        output stream float32 gain;

        event volume (float32 newVolumeDecibels)
        {
            smoothedGain.setTarget (std::levels::dBtoGain (newVolumeDecibels),
                                    max (1, int32 (smoothingTimeSeconds / float32 (processor.period))));
        }

        void main()
        {
            loop
            {
                gain <- smoothedGain.getNext();
                advance();
            }
        }

        void init()
        {
            smoothedGain.reset (1.0f);
        }

        std::smoothing::SmoothedValue smoothedGain;
    }
}

//==============================================================================
/**
    Functions for calculating the per-channel gains to use when panning a stereo signal.
*/
namespace std::pan_law
{
    /// Takes a pan position between -1 and 1 and returns a vector pair of left/right gains
    /// using a linear pan-law.
    T<2> linear<T> (T panPosition)
    {
        static_assert (T.isPrimitive && T.primitiveType.isFloat, "linear() requires a floating point argument");

        return (T(1) - panPosition,
                T(1) + panPosition);
    }

    /// Takes a pan position between -1 and 1 and returns a vector pair of left/right gains
    /// using a 3dB-centre pan-law.
    T<2> centre3dB<T> (T panPosition)
    {
        static_assert (T.isPrimitive && T.primitiveType.isFloat, "centre3dB() requires a floating point argument");

        return (sin ((T(1) - panPosition) * T(pi / 4)),
                sin ((T(1) + panPosition) * T(pi / 4)));
    }
}
